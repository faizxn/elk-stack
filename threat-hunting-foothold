
@Tactic: Initial Access

#Brute-Forcing via SSH on Jumphost#
host.name: jumphost AND event.category: authentication AND system.auth.ssh.event: Failed
host.name: jumphost AND event.category: authentication AND system.auth.ssh.event: Accepted AND source.ip: (167.71.198.43 OR 218.92.0.115) 

#Remote Code Execution on Web01#
host.name: web01 AND network.protocol: http AND destination.port: 80
host.name: web01 AND network.protocol: http AND destination.port: 80 AND source.ip: 167.71.198.43 AND http.response.status_code: 404
host.name: web01 AND network.protocol: http AND destination.port: 80 AND source.ip: 167.71.198.43 AND http.response.status_code: (200 OR 301 OR 302)

#Files Downloaded using Chrome#
host.name: WKSTN-* AND process.name: chrome.exe AND winlog.event_id: 11

#Files Opened using Outlook#
host.name: WKSTN-* AND process.name: OUTLOOK.EXE AND winlog.event_id: 11



@Tactic: Execution

host.name: WKSTN-* AND winlog.event_id: 1 AND process.name: (cmd.exe OR powershell.exe)
host.name: WKSTN-* AND winlog.event_id: 4104 
host.name: WKSTN-* AND winlog.event_id: (1 OR 3) AND (process.name: (mshta.exe OR certutil.exe OR regsvr32.exe) OR process.parent.name: (mshta.exe OR certutil.exe OR regsvr32.exe))
host.name: WKSTN-* AND winlog.event_id: (1 OR 3) AND (process.name: (*python* OR *php* OR *nodejs*) OR process.parent.name: (*python* OR *php* OR *nodejs*))

host.name: WKSTN-* AND winlog.event_id: (1 OR 3) AND process.parent.pid: 1832  



@Tactic: Defense Evasion

#Disabling Security Software#

host.name: WKSTN-* AND (*DisableRealtimeMonitoring* OR *RemoveDefinitions*)

	DisableRealtimeMonitoring - Commonly used with PowerShell's Set-MPPreference to disable its real-time monitoring.
	RemoveDefinitions - Commonly used with built-in MpCmdRun.exe to remove all existing signatures of Windows Defender.

#Log Deletion Attempts#
host.name: WKSTN-* AND winlog.event_id: 1102			|| 1102(S) The audit log was cleared
#Execution through Process Injection#
host.name: WKSTN-* AND winlog.event_id: 8



@Tactic: Persistence

#Scheduled Task Creation#
host.name: WKSTN-* AND (winlog.event_id: 4698 OR (*schtasks* OR *Register-ScheduledTask*))

#Registry Key Modification#
host.name: WKSTN-* AND winlog.event_id: 13 AND winlog.channel: Microsoft-Windows-Sysmon/Operational
host.name: WKSTN-* AND winlog.event_id: 13 AND winlog.channel: Microsoft-Windows-Sysmon/Operational AND registry.path: (*CurrentVersion\\Run* OR *CurrentVersion\\Explorer\\User* OR *CurrentVersion\\Explorer\\Shell*)
host.name: WKSTN-* AND winlog.event_id: 13 AND winlog.channel: Microsoft-Windows-Sysmon/Operational AND process.name: (reg.exe OR powershell.exe)
host.name: WKSTN-* AND (winlog.event_id: 4698 OR (*schtasks* OR *Register-ScheduledTask*))


@Tactic: Command and Control


#Command and Control over DNS#
network.protocol: dns AND NOT dns.question.name: *arpa 
network.protocol: dns AND NOT dns.question.name: *arpa AND dns.question.registered_domain: golge.xyz AND host.name: WKSTN-1 
host.name: WKSTN-1* AND destination.ip: 167.71.198.43 AND destination.port: 53


#Command and Control over Cloud Apps#
host.name: WKSTN-1* AND winlog.event_id: 1 AND process.parent.executable: "C:\\Windows\\Temp\\installer.exe"


#Command and Control over Encrypted HTTP Traffic#
network.protocol: http AND network.direction: egress  
host.name: WKSTN-* AND network.protocol: http AND network.direction: egress AND destination.domain: cdn.golge.xyz




